buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://maven.maxhenkel.de/repository/public' }
        mavenLocal()
    }
    dependencies {
        classpath group: 'de.maxhenkel.forge-update', name: 'forge-update', version: '1.0.4'
    }
}

plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'maven-publish'
    id 'net.linguica.maven-settings' version '0.5'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

apply plugin: 'forge-update'
apply plugin: 'com.modrinth.minotaur'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = archives_base_name
version = mod_version
group = maven_group

runClient.doFirst {
    args = ['--username', "henkelmax${new Random().nextInt(1000)}"]
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.fabricmc.net/' }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases" }
    maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    maven { url = 'https://maven.maxhenkel.de/repository/public' }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    mavenLocal()
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation(project(':common'))

    implementation "de.maxhenkel.voicechat:voicechat-api:${voicechat_api_version}"

    modRuntimeOnly "maven.modrinth:simple-voice-chat:fabric-${voicechat_version}"
    modRuntimeOnly "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modApi("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation "com.terraformersmc:modmenu:${modmenu_version}"

    implementation "de.maxhenkel.configbuilder:configbuilder:${configbuilder_version}"
    shadow "de.maxhenkel.configbuilder:configbuilder:${configbuilder_version}"
}

processResources {
    filesMatching("fabric.mod.json") {
        expand 'mod_id': mod_id,
                'mod_version': mod_version,
                'minecraft_version': minecraft_version,
                'minecraft_dependency': minecraft_dependency,
                'loader_version': loader_version,
                'cloth_config_version': cloth_config_version,
                'voicechat_api_version': voicechat_api_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

tasks.withType(JavaCompile) {
    source(project(':common').sourceSets.main.allSource)
}

processResources {
    from("${project(':common').projectDir}/src/main/resources")
}

java {
    withSourcesJar()
}

jar {
    from('LICENSE') {
        rename { "${it}_${archivesBaseName}" }
    }
}

curseforge {
    apiKey = file("${rootDir}/curseforge_api_key.txt").exists() ? file("${rootDir}/curseforge_api_key.txt").text : ''
    project {
        id = curse_id
        changelogType = 'markdown'
        changelog = file("changelog.md")
        releaseType = release_type
        addGameVersion curseforge_minecraft_version
        addGameVersion 'Fabric'
        mainArtifact(file("${buildDir}/libs/${archivesBaseName}-${version}.jar")) {
            displayName = "[FABRIC][${minecraft_version}] ${mod_name} ${mod_version}"
            relations {
                optionalDependency 'cloth-config'
                optionalDependency 'modmenu'
                optionalDependency 'simple-voice-chat'
            }
        }
        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

tasks.modrinth.configure({
    group = 'upload'
})

modrinth {
    token = file("${rootDir}/modrinth_token.txt").exists() ? file("${rootDir}/modrinth_token.txt").text : ''
    projectId = modrinth_id
    versionNumber = "fabric-${mod_version}"
    versionName = "[FABRIC][${minecraft_version}] ${mod_name} ${mod_version}"
    uploadFile = remapJar
    versionType = release_type.toUpperCase()
    changelog = file("changelog.md").text
    gameVersions = [minecraft_version]
    loaders = ['fabric']
    dependencies {
        optional.project "9eGKb6K1" // Simple Voice Chat
        optional.project "mOgUt4GM" // ModMenu
        optional.project "9s6osm5g" // ClothConfig
    }
    syncBodyFrom = file("${rootDir}/README.md").text
}
tasks.modrinth.dependsOn(build)

forgeUpdate {
    def messages = []
    file("changelog.md").eachLine { String line ->
        if (line.trim().startsWith('-')) {
            messages.add(line.replaceFirst('-', '').trim())
        }
    }

    serverURL = 'https://update.maxhenkel.de/'
    apiKey = file("${rootDir}/forge_update_api_key.txt").exists() ? file("${rootDir}/forge_update_api_key.txt").text : ''
    modID = mod_id
    gameVersion = minecraft_version
    modLoader = 'fabric'
    modVersion = mod_version
    updateMessages = messages
    releaseType = release_type
    tags = recommended == 'true' ? ['recommended'] : []
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier 'shadow-dev'
    relocate 'de.maxhenkel.configbuilder', "com.sonicether.soundphysics.configbuilder"
}

prepareRemapJar {
    dependsOn shadowJar
}

remapJar {
    input = shadowJar.archiveFile.get()
}

publishing {
    publications {
        soundPhysicsFabric(MavenPublication) {
            artifactId archives_base_name
            artifact(remapJar.archivePath) {
                builtBy build
                classifier null
            }
        }
    }
    repositories {
        maven {
            name = 'henkelmax.public'
            url = 'https://maven.maxhenkel.de/repository/public'
        }
    }
}